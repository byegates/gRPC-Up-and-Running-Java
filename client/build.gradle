plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.google.protobuf' version '0.9.2'
}

group 'com.wly'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

//def grpcVersion = '1.53.0'
def grpcVersion = '1.46.1'

dependencies {
    implementation "io.grpc:grpc-netty:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"

    compileOnly 'com.google.protobuf:protobuf-java:3.22.0'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
//    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.22.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets.main.java.srcDir new File(buildDir, 'generated/source')

jar {
//    configurations.implementation.setCanBeResolved(true)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "ecommerce.ProductInfoClient"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//        configurations.runtimeClasspath.collect { it.name.endsWith('jar') ? it : zipTree(it) }
    }
}


test {
    useJUnitPlatform()
}

//startScripts.enabled = false
